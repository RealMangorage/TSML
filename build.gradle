plugins {
    id 'java'
    id 'application'
    id 'net.minecraftforge.jarjar' version '+'
}

group = 'org.mangorage'
version = "$TSML_VERSION"

repositories { mavenCentral() }

configurations {
    jarJarLoader {
        canBeResolved = true
        canBeConsumed = false
    }
    jarJarMod {
        canBeResolved = true
        canBeConsumed = false
    }

}

jarJar.register() {
    def outputTxt = new File(temporaryDir, "output.txt")
    outputTxt.text = configurations.jarJarLoader.files.collect { it.name }.join(System.lineSeparator())

    from({ configurations.jarJarLoader.files }) {
        into 'JarJarLoader'
    }

    // Include the output.txt
    from(outputTxt) {
        into 'JarJarLoader'
    }
}

dependencies {
    jarJarLoader(project(":Loader"))
    jarJarLoader('io.github.classgraph:classgraph:4.8.179')
    jarJarLoader('com.google.code.gson:gson:2.10.1')

    jarJar(project(":BuiltInMods:TSMLMixin"))
    jarJar(project(":BuiltInMods:TSMLCore"))
    jarJar(project(":BuiltInMods:ExampleMod"))
}

application {
    mainClass = 'org.mangorage.tsml.bootstrap.Bootstrap'
}

// Optional runGame
tasks.register('runGame', JavaExec) {
    group = 'application'
    description = 'Runs the TSML game'

    classpath = tasks.getByName("jarJar").outputs.files
    mainClass = application.mainClass

    workingDir = file("$buildDir/run")
    doFirst { workingDir.mkdirs() }

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }

    jvmArgs '-Xms512M', '-Xmx2G'
}