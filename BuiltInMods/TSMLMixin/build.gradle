plugins {
    id 'java'
    id 'net.minecraftforge.jarjar' version '+'
}

jarJar.register()

group = 'org.mangorage'
version = "$TSML_VERSION"

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
}


dependencies {
    compileOnly(project(":Loader"))

    compileOnly('net.fabricmc:sponge-mixin:0.17.0+mixin.0.8.7')
    compileOnly('io.github.llamalad7:mixinextras-common:0.5.3')

    compileOnly("com.google.code.gson:gson:2.10.1")
    compileOnly('org.ow2.asm:asm:9.8')
    compileOnly('org.ow2.asm:asm-util:9.8')
    compileOnly('org.ow2.asm:asm-analysis:9.8')
    compileOnly('org.ow2.asm:asm-tree:9.8')
    compileOnly('org.ow2.asm:asm-commons:9.8')

    jarJar 'net.fabricmc:sponge-mixin:0.17.0+mixin.0.8.7'
    jarJar 'io.github.llamalad7:mixinextras-common:0.5.3'

    jarJar "com.google.code.gson:gson:2.10.1"
    jarJar 'org.ow2.asm:asm:9.8'
    jarJar 'org.ow2.asm:asm-util:9.8'
    jarJar 'org.ow2.asm:asm-analysis:9.8'
    jarJar 'org.ow2.asm:asm-tree:9.8'
    jarJar'org.ow2.asm:asm-commons:9.8'
}

processResources {
    // This tells Gradle: "If the version changes, this task is dirty"
    inputs.property "version", project.version

    filesMatching("tsmlmixin.mods.json") {
        expand "version": project.version
    }
}

// 1. Ensure the jarJar task actually runs during the build
tasks.build {
    dependsOn(tasks.jarJar)
}

// 2. Clear the default 'jar' from outgoing configurations and swap in 'jarJar'
def outgoingConfigurations = [configurations.apiElements, configurations.runtimeElements]

outgoingConfigurations.each { config ->
    config.outgoing.artifacts.clear() // Remove the standard jar
    config.outgoing.artifact(tasks.jarJar) // Add the jarJar as the primary artifact
}

// 3. Optional: Make the filenames look "normal" in build/libs
tasks.jar {
    archiveClassifier = 'raw'
}

tasks.jarJar {
    archiveClassifier = ''
}

artifacts {
    archives(tasks.jarJar)
}