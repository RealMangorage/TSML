plugins {
    id 'java'
}

group = 'org.mangorage'
version = "$TSML_VERSION"

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
}

configurations {
    jarJar {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    compileOnly(project(":Loader"))

    compileOnly('net.fabricmc:sponge-mixin:0.17.0+mixin.0.8.7')
    compileOnly('io.github.llamalad7:mixinextras-common:0.5.3')

    compileOnly("com.google.code.gson:gson:2.10.1")
    compileOnly('org.ow2.asm:asm:9.8')
    compileOnly('org.ow2.asm:asm-util:9.8')
    compileOnly('org.ow2.asm:asm-analysis:9.8')
    compileOnly('org.ow2.asm:asm-tree:9.8')
    compileOnly('org.ow2.asm:asm-commons:9.8')

    jarJar 'net.fabricmc:sponge-mixin:0.17.0+mixin.0.8.7'
    jarJar 'io.github.llamalad7:mixinextras-common:0.5.3'

    jarJar "com.google.code.gson:gson:2.10.1"
    jarJar 'org.ow2.asm:asm:9.8'
    jarJar 'org.ow2.asm:asm-util:9.8'
    jarJar 'org.ow2.asm:asm-analysis:9.8'
    jarJar 'org.ow2.asm:asm-tree:9.8'
    jarJar'org.ow2.asm:asm-commons:9.8'
}

processResources {
    // This tells Gradle: "If the version changes, this task is dirty"
    inputs.property "version", project.version

    filesMatching("tsml.mods.json") {
        expand "version": project.version
    }
}

jar {

    // Build output.txt during configuration, not execution
    def outputTxt = new File(temporaryDir, "output.txt")
    outputTxt.text = configurations.jarJar.files.collect { it.name }.join(System.lineSeparator())

    archiveBaseName.set('TSMLMixin')
    archiveClassifier.set('') // no classifier, replaces default jar

    // Include compiled classes
    from sourceSets.main.output

    // Include compileOnly jars inside JarJar/
    from {
        configurations.jarJar
    } {
        into 'JarJar'
    }

    // Include the output.txt
    from(outputTxt) {
        into 'JarJar'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}